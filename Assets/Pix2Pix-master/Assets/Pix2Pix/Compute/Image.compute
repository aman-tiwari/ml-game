// Image pre/post-processing kernels
// https://github.com/keijiro/Pix2Pix

#pragma enable_d3d11_debug_symbols

#pragma kernel ImageToTensor
#pragma kernel TensorToImage

uint3 Shape;

Texture2D<float3> InputImage;
RWBuffer<float> OutputTensor;

Buffer<float> InputTensor;
RWTexture2D<float4> OutputImage;

[numthreads(16, 16, 1)]
void ImageToTensor(uint2 id : SV_DispatchThreadID)
{
    float3 p = InputImage[id] * 2 - 1;

    uint i = Shape.z * (id.x + Shape.y * (Shape.x - 1 - id.y));

    OutputTensor[i++] = p.x;
    OutputTensor[i++] = p.y;
    OutputTensor[i  ] = p.z;
}

[numthreads(16, 16, 1)]
void TensorToImage(uint2 id : SV_DispatchThreadID)
{
    uint i = Shape.z * (id.x + Shape.y * (Shape.x - 1 - id.y));

    float r = InputTensor[i++];
    float g = InputTensor[i++];
    float b = InputTensor[i  ];

    OutputImage[id] = (float4(r, g, b, 1) + 1) / 2;
}
